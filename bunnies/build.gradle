apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'io.fabric'

androidExtensions {
    experimental = true
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.fueledbycaffeine.bunnypedia"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 25
        versionName "0.18"

        multiDexEnabled true
    }

    dexOptions {
        preDexLibraries true
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        quiet true
        lintConfig rootProject.file('lint.xml')

        warningsAsErrors true
        checkDependencies true
        checkTestSources true
        explainIssues false

        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            def password = getPassword(defaultConfig.applicationId)
            storeFile file("$rootProject.projectDir/bunnypedia.jks")
            storePassword password
            keyAlias 'bunnypedia'
            keyPassword password
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release

            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    // Support
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.preference:preference:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:1.0.0"
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'androidx.core:core-ktx:1.0.1'

    def archComponentsVersion = "2.1.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-runtime:$archComponentsVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$archComponentsVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archComponentsVersion"
    def navVersion = "2.1.0-alpha02"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    def roomVersion = "2.1.0-alpha06"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    def pagingVersion = "2.1.0"
    implementation "androidx.paging:paging-runtime:$pagingVersion"
    implementation "androidx.paging:paging-rxjava2:$pagingVersion"
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'

    def epoxyVersion = "3.4.1"
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"

    // Dependency injection
    def daggerVersion = "2.22"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    def rxBindingVersion = "3.0.0-alpha2"
    implementation "com.jakewharton.rxbinding3:rxbinding:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxBindingVersion"

    def glideVersion = '4.9.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "me.saket:better-link-movement-method:2"
    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Date/Time
    implementation 'net.danlew:android.joda:2.10.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def ankoVersion = "0.10.8"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-sdk23:$ankoVersion"

    // https://docs.fabric.io/android/changelog.html#crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }
}

def getPassword(String item) {
    if (System.getenv('CI') != null) {
        System.getenv('ANDROID_KEYSTORE_PASSWORD')
    } else {
        def stdout = new ByteArrayOutputStream()
        def stderr = new ByteArrayOutputStream()
        exec {
            commandLine 'security', '-q', 'find-generic-password', '-s', item, '-w'
            standardOutput = stdout
            errorOutput = stderr
            ignoreExitValue true
        }
        //noinspection GroovyAssignabilityCheck
        stdout.toString().trim()
    }
}
