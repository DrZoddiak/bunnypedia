apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

androidExtensions {
    experimental = true
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.fueledbycaffeine.bunnypedia"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 20
        versionName "0.13"

        multiDexEnabled true
    }

    dexOptions {
        preDexLibraries true
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')

        checkDependencies true
        checkTestSources true
        explainIssues false

        // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            def password = getPassword(defaultConfig.applicationId)
            storeFile file("$rootProject.projectDir/bunnypedia.jks")
            storePassword password
            keyAlias 'bunnypedia'
            keyPassword password
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release

            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    // Support
    def supportVersion = "1.0.0"
    implementation "androidx.appcompat:appcompat:$supportVersion"
    implementation "androidx.preference:preference:$supportVersion"
    implementation "androidx.recyclerview:recyclerview:$supportVersion"
    implementation "androidx.cardview:cardview:$supportVersion"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:1.0.0"
    implementation 'com.google.android:flexbox:1.0.0'

    def archComponentsVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime:$archComponentsVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$archComponentsVersion"
    def roomVersion = "2.0.0-rc01"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.paging:paging-runtime:$roomVersion"
    implementation "androidx.paging:paging-rxjava2:$roomVersion"
    implementation 'com.github.humazed:RoomAsset:1.0.3'

    // Dependency injection
    def daggerVersion = "2.16"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    def rxBindingVersion = "2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxBindingVersion"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxBindingVersion"

    def glideVersion = '4.8.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "me.saket:better-link-movement-method:2"
    implementation 'com.futuremind.recyclerfastscroll:fastscroll:0.2.5'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Date/Time
    implementation 'net.danlew:android.joda:2.9.9.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def ankoVersion = "0.10.5"
    implementation "org.jetbrains.anko:anko-commons:$ankoVersion"
    implementation "org.jetbrains.anko:anko-sdk23:$ankoVersion"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}

def getPassword(String item) {
    if (System.getenv('CI') != null) {
        System.getenv('ANDROID_KEYSTORE_PASSWORD')
    } else {
        def stdout = new ByteArrayOutputStream()
        def stderr = new ByteArrayOutputStream()
        exec {
            commandLine 'security', '-q', 'find-generic-password', '-s', item, '-w'
            standardOutput = stdout
            errorOutput = stderr
            ignoreExitValue true
        }
        //noinspection GroovyAssignabilityCheck
        stdout.toString().trim()
    }
}
