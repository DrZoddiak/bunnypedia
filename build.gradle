// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.31.2'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.1.0"
    }
}

plugins {
    // Variables can't be used in 'plugins' block,
    // so duplication of detekt version string is unavoidable in Gradle Groovy DSL for now.
    // Others experiencing it:  https://github.com/gradle/gradle/issues/3593
    // Mozilla decided to live with the duplication:  https://github.com/mozilla-mobile/focus-android/issues/1886
    // Possible solution that requires changing to Gradle Kotlin DSL:  https://github.com/gradle/gradle/issues/1697
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC16"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            // TODO: AGP 4.0 remove threetenabp in favor of this
//            coreLibraryDesugaringEnabled true
            allWarningsAsErrors = true
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    def detektVersion = "1.0.0-RC16"

    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        toolVersion = detektVersion
        config = files("$rootProject.projectDir/detekt-config.yml")
        baseline = file("$rootProject.projectDir/detekt-baseline.xml")

        reports {
            html {
                enabled = true
                destination = file("$project.buildDir/reports/detekt.html")
            }
        }
    }

    // enable detekt formatting rules
    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.0-RC16"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
